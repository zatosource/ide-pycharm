plugins {
    id "org.jetbrains.intellij" version "0.4.14"
}

repositories {
    mavenCentral()
}

version = '1.0.3'

allprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        pluginName 'zato'

        version 'IC-2016.3.4'
        plugins 'PythonCore:2016.3.163.298'

// use these properties instead to build against IJ 2018.1
//        version 'IC-2018.1.8'
//        plugins 'PythonCore:2018.1.181.5087.50'

// use these properties instead to build against IJ 2019.3
//        version 'IC-193.5662.31-EAP-SNAPSHOT'
//        plugins 'PythonCore:193.5662.31'

        downloadSources true
    }

    patchPluginXml {
        sinceBuild '163.0'
        untilBuild '193.*'
    }

    publishPlugin {
        username 'your-jetbrains-username'
        //add your user's password on the command line using '-Dintellij.publish.password=...'
        password System.properties['intellij.publish.password']

        //use a custom channel if you'd offer nightly/unstable versions
        //channels 'eap'
    }

    // code coverage using the Jacoco engine, see https://docs.gradle.org/current/userguide/jacoco_plugin.html
    // the coverage reports will be available at build/reports/coverage/test/html/index.html
    apply plugin: 'jacoco'
    jacoco {
        toolVersion = "0.8.5"
        reportsDir = file("$buildDir/reports/coverage")
    }
    test {
        finalizedBy jacocoTestReport
        jacoco {
            enabled true
            includes["com.*"]
        }
    }

    dependencies {
        testCompile "org.nanohttpd:nanohttpd:2.2.0"
        testCompile "junit:junit:4.12"
    }

    // test logging configuration, shows test progress and messages to standard error
    // see https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/testing/logging/TestLogEvent.html
    test {
        testLogging {
            events = [
                    org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED,
                    org.gradle.api.tasks.testing.logging.TestLogEvent.PASSED,
                    org.gradle.api.tasks.testing.logging.TestLogEvent.SKIPPED,
                    org.gradle.api.tasks.testing.logging.TestLogEvent.STANDARD_ERROR
            ]
        }
    }
}
